// localCore.at
// Mathijs Saey
// The local core object is the general file that imports the different network 
// and local modules and calls them when necessary, it also contains the 
// local instance of the active game.

import ~.local.game;
import ~.local.team;
import ~.local.player;

import ~.network.merger;
import ~.network.sharing;
import ~.network.requests;
import ~.network.exporter;
import ~.network.importer;

import /.at.lang.futures;

/**************** 
 * Network Game *
 ****************/

/**
 A networked game is a subclass around the game class
 that stores the extra information that might be needed
 for some network classes.
*/
def networkGame := extend: game with: {
	def requestHandler := requestHandler.new();
};

/*********
 * Core  *
 *********/

 // => in game timestamp bijhouden van laatste change
 // => listener aan kopellen via future, bij elke change reexporten

def core := object: { 
	def currentGame;
	def localPlayer;

	/************
	 * External *
	 ************/

	// Initialises the core with the player name
	def init(playerName) {
		self.localPlayer := player.new(playerName,1);
		self;
	};

	// Start a new game
	def createGame(gameName, teamNames) {
		def deviceID := localPlayer.name;
		self.currentGame := networkGame.new(gameName, teamNames, localPlayer, deviceID);
		self.startGame();
	};

	// Join a game (if it's in range)
	def joinGame(gameIdx, teamIdx) {
		localPlayer.teamIdx := teamIdx;
		when: importer.joinGame(gameIdx, self.localPlayer, networkGame) 
		becomes: { |game|
			if: game != nil then: {
				currentGame := game;
				self.startGame();	
			};
		};
		nil;
	};

	// Get the names of the games that are in range
	def getGameNames() {
		importer.search();
		importer.gameNames();
	};

	/********
	 * Util *
	 ********/

	// Starts the necessary services
	// after the game has been joined/created
	def startGame() {
		share.init(currentGame, requestSender.new());
		merger.init(currentGame);
		exporter.init(currentGame);
		self;
	};

	// Used to get external access to share and exporter
	// Mainly used for testing.
	def getShare() {share};
	def getExport() {exporter};

};

/***************
 * Export List *
 ***************/
network.online();
object: { 
	def core := core; 
};