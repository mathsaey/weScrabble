// merger.at
// Mathijs Saey
// The import module is responsible for updating both games
// when 2 players meet each other

import /.at.lang.futures;

import /.weScrabble.local.game;
import /.weScrabble.local.team;

/**********
 * Merger *
 **********/

 def merger := object: { 
 	def subscriptions := [];
 	def id;

 	def init(id) {
 		self.id := id;
 		self;
 	};

 	def runUpdater(core) {
 		subscriptions := subscriptions + [
 		whenever: Game discovered: { |game|
 			def futureID := game<-id()@TwoWay;

	 		subscriptions := subscriptions + [
 			when: futureID becomes: {|id|
 				if: id == core.currentGame.id then: {
 					system.println("MERGER: merger discovered game");
 					def futureTeams := game<-teams()@TwoWay;

	 				subscriptions := subscriptions + [
 					when: futureTeams becomes: {|teams|
 						teams.length.doTimes: { |idx|
 							system.println("merging: " + teams[idx].name);
 							core.currentGame.teams[idx].updateTeam: teams[idx];
 						}
 					}]
 				};
 			}]
 		}];
 		nil;
 	};
 };

/***************
 * Export List *
 ***************/

 object: { 
 	def merger := merger; 
 };