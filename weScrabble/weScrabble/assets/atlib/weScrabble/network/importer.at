// importer.at
// Mathijs Saey
// The import module is responsible for finding and joining games.

import /.at.lang.exceptions;
import /.at.lang.futures;

import /.weScrabble.local.game;
import /.weScrabble.local.team;
import /.weScrabble.local.player;

// Exceptions
deftype InvalidIndexEception <: lobby.at.lang.types.Exception;
def  XInvalidIndexEception := createException(InvalidIndexEception);

/************       
 * Importer *
 ************/

def importer := object: {
	def subscriptions := [];
	def gameNames := [];
	def games := [];

	// Cancels all current subscriptions
	def endSearch() {
 		foreach: { |s|
 			 s.cancel();
 		} in: subscriptions;
 		subscriptions := [];
 	};

 	// Join game at index
 	def joinGame(idx, localPlayer) {
 		def [futureGame, resGame] := makeFuture();
 		def tmpGame := clone: game;
 		def gameRef := games[idx];
 		tmpGame.localPlayer := localPlayer;

 		//Ensure the game is currently reachable
 		if: gameRef == 0 then: {
 			raise: XInvalidIndexEception.new("No valid player found at idx " + idx);
 		};

 		// Get some data about the remote game
 		def futureID := gameRef<-id()@TwoWay;
 		def futureName := gameRef<-name()@TwoWay;
 		def futureTeams := gameRef<-teams()@TwoWay;

 		when: (group: [futureID, futureName, futureTeams]) becomes: { |values|
 			def [id,n,t] := values;
 			tmpGame.id := id;
			tmpGame.name := n;
			tmpGame.teams := t;
			tmpGame.teams[localPlayer.teamIdx()].addPlayer: localPlayer;

			resGame.resolve(tmpGame);
 		};

 		// Add localplayer to the remote game 
 		// TODO: should contain a way to ensure player got added to remote game
 		gameRef<-addPlayer: localPlayer withIdx: localPlayer.teamIdx();

 		// Stop looking for new games
 		self.endSearch();
 		futureGame;
 	};

 	// Start looking for available games
 	def search() {
 		// Ensure we only have one active search going
 		if: subscriptions == [] then: {
 			// When discovering a game, ensure we haven't stored it before
 			subscriptions := subscriptions + [
 			whenever: ActiveGame discovered: { |game|
 			if:  !games.contains(game) then: {
 				def nameF := game<-name()@TwoWay;
 				def name;
 				def idx;

 				when: nameF becomes: {|n|
 					name := n;
 					gameNames := gameNames + [name];
 					games := games + [game];
 					idx := games.length;
 				};

 				subscriptions := subscriptions + [
 				whenever: game reconnected: {
 					if: name != nil then: {
 						gameNames[idx] := name;
 					};
 					games[idx] := game;
 				}];

 				subscriptions := subscriptions + [
 				whenever: game disconnected: {
 					if: name != nil then: {
 						gameNames[idx] := gameNames[idx] + " <Disconnected>";
 						games[idx] := 0;
 					};
 				}];
 			}}];
 		};
 	};
};

/***************
 * Export List *
 ***************/

object: {
	def importer := importer;
};