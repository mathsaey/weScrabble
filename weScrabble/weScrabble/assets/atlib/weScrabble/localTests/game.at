// game.at
// Mathijs Saey
// This file contains the implementation of the game unit tests

// It's worth noting that these tests (and in fact all the tests in localTests folder)
// only perform testing from a local point of view.

// only works when adding weScrabble as a path element
// from the weScrabble folder: $ iat localTests/game.at -o weScrabble=.

import /.weScrabble.local.game;
import /.weScrabble.local.player;

def gameUnitTest := extend: /.at.unit.test.UnitTest.new("game unit test") with: {
	def testP1;
	def testP2;
	def testGame;

	def setUp() {
		testP1 := player.new("testPlayer 1", 1);
		testP2 := player.new("testPlayer 2", 2);
		testGame := game.init("testGame", ["red", "blue"], testP1, "testId");
	};

	def tearDown() {
		testP1 := nil;
		testP2 := nil;
		testGame := nil;
	};

	def testInit() {
		self.assertNotNil(testGame.id());
		self.assertNotNil(testGame.name());
		self.assertNotNil(testGame.teams());
		self.assertNotNil(testGame.localPlayer());
	};

	def testAddPlayer() {
		testGame.addPlayer(testP2, 2);
		self.assertTrue(testGame.teams[2].activePlayers[1] == testP2);
	};

	def testDidTeamWin() {
		testGame.localPlayer.resolve();
		self.assertTrue(testGame.didTeamWin(1));
	};

	def testIsActive() {
		self.assertTrue(testGame.isActive());
		testGame.localPlayer.resolve();
		self.assertFalse(testGame.isActive());
	};
};

object: { 
	def gameUnitTest := gameUnitTest;
};